plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.vanniktech.maven.publish' version '0.34.0'
    // id 'org.springframework.boot' version '3.5.5' apply false
}

group = project.findProperty("GROUP") ?: group
version = project.findProperty("VERSION_NAME") ?: version

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.5'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.matching {
    it.name == "bootJar"
}.configureEach {
    enabled = false
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Springify Multiconfig',
                'Implementation-Version': version
        )
    }
}

publishing {
    publications {
        withType(MavenPublication).configureEach {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

tasks.matching {
    it.name in ['plainJavadocJar', 'plainSourcesJar']
}.configureEach {
    enabled = false
}

def docAndSourceTasks = tasks.matching {
    it.name in ['javadocJar', 'sourcesJar',
            'plainJavadocJar', 'plainSourcesJar']
}
tasks.matching {
    it.name.startsWith('generateMetadataFileFor') &&
            it.name.endsWith('Publication')
}.configureEach {
    dependsOn(docAndSourceTasks)
}

mavenPublishing {
    publishToMavenCentral(true)
    signAllPublications()
    pom {
        name = project_name
        description = project_description
        url = project_url
        licenses {
            license {
                name = project_license_slug
                url = project_license_url
            }
        }
        developers {
            developer {
                id = project_developer
                name = project_developer
            }
        }
        scm {
            connection = project_scm
            developerConnection = project_scm
            url = project_url
        }
    }
}

signing {
    def key = findProperty('signingInMemoryKey')?.replace('\\n', '\n')
    def keyPass = findProperty('signingInMemoryKeyPassword')
    useInMemoryPgpKeys(key as String, keyPass as String)
}

afterReleaseBuild.dependsOn 'publishToMavenCentral'
